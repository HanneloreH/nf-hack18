= Operators 

* Built-in functions applied to channels
* Transform channels content
* Can be used also to filter, fork and combine channels

== Basic example

[source,nextflow,linenums]
----
nums = Channel.from(1,2,3,4)         // <1>
square = nums.map { it -> it * it }  // <2> 
square.println()                     // <3>
----

image::channel-map.png[]

<1> Create a queue channel emitting four values.
<2> Create a new channels transforming each number in it's square.
<3> Print the channel content.

Operators can be chained to implement custom behaviors:

[source,nextflow,linenums]
----
Channel.from(1,2,3,4)
        .map { it -> it * it }
        .println()
----

Operators can be separated in to five groups:

* Filtering operators
* Transforming operators
* Splitting operators
* Combining operators
* Forking operators
* Maths operators


== Basic operators 

=== println 

The `println` operator prints the items emitted by a channel to the console standard output appending a 
_new line_ character to each of them. For example:

[source,nextflow,linenums]
----
Channel
      .from('foo', 'bar', 'baz', 'qux')
      .println()
----

It prints:

```
foo
bar
baz
qux
```

An optional _closure_ parameter can be specified to customise how items are printed. For example:

[source,nextflow,linenums]
----
Channel
      .from('foo', 'bar', 'baz', 'qux')
      .println { "- $it" }
----

It prints:

----
- foo
- bar
- baz
- qux
----

IMPORTANT: `println` is a terminal operator, i.e. it does not allow the chaining with other operators. 

=== map 

The `map` operator applies a function of your choosing to every item emitted by a channel, and returns the items so obtained as a new channel. The function applied is called the _mapping_ function and is expressed with a _closure_ as shown in the example below:

[source,nextflow,linenums]
----
Channel
    .from( 'hello', 'world' )
    .map { it -> it.reverse() }
    .println()
----

A `map` can associate to each element a generic _tuple_ containing any data as needed. 

[source,nextflow,linenums]
----
Channel
    .from( 'hello', 'world' )
    .map { word -> [word, word.size()] }
    .println { word, len -> "$word contains $len letters" }
----

==== Exercise

Use `fromPath` to create a channel emitting the _fastq_ files matching the pattern `data/ggal/*.fq`, 
then chain with a map to return a pair containing the file name and the path itself. 
Finally print the resulting channel. 

[source,nextflow,linenums]
----
Channel.fromPath('data/ggal/*.fq')
        .map { file -> [ file.name, file ] }
        .println { name, file -> "> file: $name" }
----


=== into

The `into` operator connects a source channel to two or more target channels in such a way the values emitted by the source channel are copied to the target channels. For example:

[source,nextflow,linenums]
----
Channel
     .from( 'a', 'b', 'c' )
     .into{ foo; bar }

foo.println{ "Foo emits: " + it }
bar.println{ "Bar emits: " + it }
----

TIP: Note the use in this example of curly brackets and the `;` as channel names separator. This is needed because the actual parameter of into is a _closure_ which defines the target channels to which the source one is connected.


=== mix 

The `mix` operator combines the items emitted by two (or more) channels into a single channel.

[source,nextflow,linenums]
----
c1 = Channel.from( 1,2,3 )
c2 = Channel.from( 'a','b' )
c3 = Channel.from( 'z' )

c1 .mix(c2,c3).println()
----

```
1
2
a
3
b
z
```

IMPORTANT: The items in the resulting channel have the same order as in respective original channel, 
however there's no guarantee that the element of the second channel are append after the elements 
of the first. Indeed in the above example the element `a` has been printed before `3`. 

=== flatten 

The `flatten` operator transforms a channel in such a way that every _tuple_ is flattened so that each single entry is emitted as a sole element by the resulting channel.

[source,nextflow,linenums]
----
foo = [1,2,3]
bar = [4, 5, 6]

Channel
    .from(foo, bar)
    .flatten()
    .println()
----

The above snippet prints:

```
1
2
3
4
5
6
```

=== collect 

The `collect` operator collects all the items emitted by a channel to a list and return the resulting object as a sole emission.

[source,nextflow,linenums]
----
Channel
    .from( 1, 2, 3, 4 )
    .collect()
    .println()
----

It prints a single value:

```
[1,2,3,4]
```

TIP: The result of the `collect` operator is a *value* channel.


=== groupTuple

The `groupTuple` operator collects tuples (or lists) of values emitted by the source channel grouping together the elements that share the same key. Finally it emits a new tuple object for each distinct key collected.

Try the following example:

[source,nextflow,linenums]
----
Channel
     .from( [1,'A'], [1,'B'], [2,'C'], [3, 'B'], [1,'C'], [2, 'A'], [3, 'D'] )
     .groupTuple()
     .println()
----

It shows: 

```
[1, [A, B, C]]
[2, [C, A]]
[3, [B, D]]
```

This operator is useful to process altogether all elements for which there's a common 
property or a grouping key. 

==== Exercise 

Use `fromPath` to create a channel emitting the _fastq_ files matching the pattern `data/ggal/*.fq`, 
then use a `map` to associate to each file the name prefix. Finally group together all 
files having the same common prefix. 


=== join 

The `join` operator creates a channel that joins together the items emitted by two channels for which exits a matching key. The key is defined, by default, as the first element in each item emitted.

[source,nextflow,linenums]
----
left = Channel.from(['X', 1], ['Y', 2], ['Z', 3], ['P', 7])
right= Channel.from(['Z', 6], ['Y', 5], ['X', 4])
left.join(right).println()
----

The resulting channel emits:

```
[Z, 3, 6]
[Y, 2, 5]
[X, 1, 4]
```

=== subscribe 

The `subscribe` operator permits to execute a user define function each time a new value is emitted by the source channel.

The emitted value is passed implicitly to the specified function. For example:

[source,nextflow,linenums]
----
// define a channel emitting three values
source = Channel.from ( 'alpha', 'beta', 'delta' )

// subscribe a function to the channel printing the emitted values
source.subscribe {  println "Got: $it"  }
----

It prints: 

----
Got: alpha
Got: beta
Got: delta
----



IMPORTANT: `subscribe` is a terminal operator, i.e. it does not allow the chaining with other operators. 

== More resources 

Check the https://www.nextflow.io/docs/latest/operator.html[operators documentation] on Nextflow web site. 


