= Processes 

A _process_ is the basic Nextflow computing primitive to execute foreign function i.e. custom 
scripts or tools.

The process definition starts with keyword the ``process``, followed by process name and finally the process `body` delimited by curly brackets. The process body must contain a string which represents the command or, more generally, a script that is executed by it. 

A basic process looks like the following example:


[source,nextflow,linenums]
----
process sayHello {
  """
  echo 'Hello world!'
  """
}
----

A process may contain five definition blocks, respectively: directives,
inputs, outputs, when clause and finally the process script. The syntax is defined as follows:

----
process < name > {
  [ directives ]        // <1>
  input:                // <2>
  < process inputs >    
  output:               // <3>
  < process outputs >
  when:                 // <4>
  < condition >         
  [script|shell|exec]:  // <5>
  < user script to be executed >
}
----

<1> Zero, one or more process directives 
<2> Zero, one or more process inputs
<4> Zero, one or more process outputs
<4> An optional boolean conditional to trigger the process execution 
<5> The command to be executed

== Script

The `script` block is a string statement that defines the command that is executed by the process to carry out its task.

A process contains one and only one script block, and it must be the last statement when the process contains input and output declarations.

The script block can be a simple string or multi-line string. The latter simplifies the writing of non trivial scripts
composed by multiple commands spanning over multiple lines. For example::

[source,nextflow,linenums]
----
process example {
    script:
    """
    blastp -db /data/blast -query query.fa -outfmt 6 > blast_result
    cat blast_result | head -n 10 | cut -f 2 > top_hits
    blastdbcmd -db /data/blast -entry_batch top_hits > sequences
    """
}
----

By default the process command is interpreted as a *Bash* script. However any other scripting language can be used just simply starting the script with the corresponding https://en.wikipedia.org/wiki/Shebang_(Unix)[Shebang] declaration. For example: 

[source,nextflow,linenums]
----
process pyStuff {
  script:
  """
  #!/usr/bin/env python

  x = 'Hello'
  y = 'world!'
  print "%s - %s" % (x,y)
  """
}
----

TIP: This allows the compositing in the same workflow script of tasks using different programming languages which may better fit a particular job. However for large chunks of code is suggested to save them 
into separate files and invoke them from the process script. 

=== Script variables

Process script can be defined dynamically using variable values like in other string. 

[source,nextflow,linenums]
----
params.data = 'World'

process foo {
  script:
  """
  echo Hello $params.data
  """
}
----

IMPORTANT: Since Nextflow uses the same Bash syntax for variable substitutions in strings, Bash environment variables need to be escaped using `\` character.

[source,nextflow,linenums]
----
process foo {
  script:
  """
  echo \$PATH | tr : '\\n'
  """
}
----

This can be tricky when the script uses many Bash variables. A possible alternative 
is to use a script string delimited by single-quote characters

[source,nextflow,linenums]
----
process bar {
  script:
  '''
  echo $PATH | tr : '\\n'
  '''
}
----

However this won't allow any more the usage of Nextflow variables in the command script. 

Another alternative is to use a `shell` statement instead of `script` which uses a different 
syntax for Nextflow variable: `!{..}`. This allow to use both Nextflow and Bash variables in 
the same script. 

[source,nextflow,linenums]
----
params.data = 'le monde'

process baz {
  shell:
  '''
  X='Bonjour' 
  echo $X !{params.data}
  '''
}
----

==== Exercise 

Write a Nextflow script, putting together the above example, printing either a Nextflow variables and a Bash variable both the `script` and `shell` syntax. 

=== Conditional script 

The process script can also be defined in a complete dynamic manner using a `if` statement or any other expression evaluating to string value. For example: 

[source,nextflow,linenums]
----
params.aligner = 'kallisto'

process foo {
  script:
  if( params.aligner == 'kallisto' ) 
    """
    kallisto --reads /some/data.fastq
    """
  else if( params.aligner == 'salmon' ) 
    """
    salmons --reads /some/data.fastq
    """
  else 
    throw new IllegalArgumentException("Unknown aligner $params.aligner")
}
----

==== Exercise

Write a custom function that given the aligner name as parameter returns the command 
string to be executed. Then use this function as the process script body. 

== Inputs 

Nextflow processes are isolated from each other but can communicate between themselves sending values through channels.

The input block defines which channels the process is expecting to receive inputs data from. You can only define one input block at a time and it must contain one or more inputs declarations.

The input block follows the syntax shown below:

```nextflow
input:
  <input qualifier> <input name> [from <source channel>] 
```

=== Input values 

The `val` qualifier allows you to receive data of any type as input. It can be accessed in the process script by using the specified input name, as shown in the following example:

[source,nextflow,linenums]
----
num = Channel.from( 1, 2, 3 )

process basicExample {
  input:
  val x from num

  """
  echo process job $x
  """
}
----

In the above example the process is executed three times, each time a value is received from the channel num and used to process the script. Thus, it results in an output similar to the one shown below:

```
process job 3
process job 1
process job 2
```

IMPORTANT: The channel guarantees that items are delivered in the same order as they have been sent - but - since the process is executed in a parallel manner, there is no guarantee that they are processed in the same order as they are received.

=== Input files

The `file` qualifier allows the handling of file values in the process execution context. This means that
Nextflow will stage it in the process execution directory, and it can be access in the script by using the name specified in the input declaration.


[source,nextflow,linenums]
----
reads = Channel.fromPath( 'data/ggal/*.fq' )

process foo {
    input:
    file 'sample.fastq' from reads
    script:
    """
    your_command --reads sample.fastq
    """
}
----

The input file name can also be defined using a variable reference as shown below: 

[source,nextflow,linenums]
----
reads = Channel.fromPath( 'data/ggal/*.fq' )

process foo {
    input:
    file sample from reads
    script:
    """
    your_command --reads $sample
    """
}
----

The same syntax it's also able to handle more than one input file in the same execution. 
Only change the channel composition. 

[source,nextflow,linenums]
----
reads = Channel.fromPath( 'data/ggal/*.fq' )

process foo {
    input:
    file sample from reads.collect()
    script:
    """
    your_command --reads $sample
    """
}
----

==== Exercise 

Write a script that merge all read files matching the pattern `data/ggal/*_1.fq` into a single
file. Then print the first 20 lines. 

=== Combine input channels

A key feature of processes is the ability to handle inputs from multiple channels. However it's 
important to understands how the content of channel and their semantic affect the execution 
of a process. 

Consider the following example: 

[source,nextflow,linenums]
----
process foo {
  echo true
  input:
  val x from Channel.from(1,2,3)
  val y from Channel.from('a','b','c')
  script:
   """
   echo $x and $y
   """
}
----

Both channels emit three value, therefore the process is executed three times, each time with a different pair: 

* (1, a)
* (2, b)
* (3, c)

What is happening is that the process waits until there's a complete input configuration i.e. it receives an input value from all the channels declared as input.

When this condition is verified, it consumes the input values coming from the respective channels, and spawns a task execution, then repeat the same logic until one or more channels have no more content.

This means channel values are consumed serially one after another and the first empty channel cause the process execution to stop even if there are other values in other channels.

*What does it happen when not all channels have the same cardinality (i.e. they emit a different number of elements)?*

For example: 

[source,nextflow,linenums]
----
process foo {
  echo true
  input:
  val x from Channel.from(1,2)
  val y from Channel.from('a','b','c','d')
  script:
   """
   echo $x and $y
   """
}
----

In the above example the process is executed only two time, because when a channel has no more data to be processed it stops the process execution. 

IMPORTANT: Note however that _value_ channel do not affect the process termination. 

To better understand this behavior compare the previous example with the following one:

[source,nextflow,linenums]
----
process bar {
  echo true
  input:
  val x from Channel.value(1)
  val y from Channel.from('a','b','c')
  script:
   """
   echo $x and $y
   """
}
----

==== Exercise 

Write a process that is executed for each read file matching the pattern `data/ggal/*_1.fa` and 
and use the same `data/ggal/transcriptome.fa` in each execution. 

=== Input repeaters 

The `each` qualifier allows you to repeat the execution of a process for each item in a collection, every time a new data is received. For example:

[source,nextflow,linenums]
----
sequences = Channel.fromPath('*.fa')
methods = ['regular', 'expresso', 'psicoffee']

process alignSequences {
  input:
  file seq from sequences
  each mode from methods

  """
  t_coffee -in $seq -mode $mode
  """
}
----

In the above example every time a file of sequences is received as input by the process, it executes three tasks running a T-coffee alignment with a different value for the `mode` parameter. This is useful when you need to repeat the same task for a given set of parameters.

==== Exercise 

Extend the previous example so a task is executed for each read file matching the pattern `data/ggal/*_1.fa` and repeat the same task both with `salmon` and `kallisto`. 


== Outputs 

The _output_ declaration block allows to define the channels used by the process to send out the results produced.

It can be defined at most one output block and it can contain one or more outputs declarations. The output block follows the syntax shown below:

----
output:
  <output qualifier> <output name> [into <target channel>[,channel,..]]
----

=== Output values

The `val` qualifier allows to output a _value_ defined in the script context. In a common usage scenario,
this is a value which has been defined in the _input_ declaration block, as shown in the following example::

[source,nextflow,linenums]
----
methods = ['prot','dna', 'rna']

process foo {
  input:
  val x from methods

  output:
  val x into receiver

  """
  echo $x > file
  """
}

receiver.println { "Received: $it" }
----

=== Output files

The `file` qualifier allows to output one or more files, produced by the process, over the specified channel.

[source,nextflow,linenums]
----
process randomNum {

    output:
    file 'result.txt' into numbers

    '''
    echo $RANDOM > result.txt
    '''
}

numbers.println { "Received: " + it.text }
----

In the above example the process `randomNum` creates a file named `result.txt` containing a random number.

Since a file parameter using the same name is declared in the output block, when the task is completed that
file is sent over the `numbers` channel. A downstream `process` declaring the same channel as _input_ will
be able to receive it.


=== Multiple output files

When an output file name contains a `*` or `?` wildcard character it is interpreted as a 
http://docs.oracle.com/javase/tutorial/essential/io/fileOps.html#glob[glob] path matcher. 
This allows to _capture_ multiple files into a list object and output them as a sole emission. For example:

[source,nextflow,linenums]
----
process splitLetters {

    output:
    file 'chunk_*' into letters

    '''
    printf 'Hola' | split -b 1 - chunk_
    '''
}

letters
    .flatMap()
    .println { "File: ${it.name} => ${it.text}" }
----

it prints:

----
File: chunk_aa => H
File: chunk_ab => o
File: chunk_ac => l
File: chunk_ad => a
----

Some caveats on glob pattern behavior:

* Input files are not included in the list of possible matches.
* Glob pattern matches against both files and directories path.
* When a two stars pattern ``**`` is used to recourse across directories, only file paths are matched
  i.e. directories are not included in the result list.

==== Exercise 

Remove the `flatMap` operator and see out the output change. The documentation 
for the `flatMap` operator is available at https://www.nextflow.io/docs/latest/operator.html#flatmap[this link].

=== Dynamic output file names

When an output file name needs to be expressed dynamically, it is possible to define it using a dynamic evaluated
string which references values defined in the input declaration block or in the script global context.
For example::

[source,nextflow,linenums]
----
process align {
  input:
  val x from species
  file seq from sequences

  output:
  file "${x}.aln" into genomes

  """
  t_coffee -in $seq > ${x}.aln
  """
}
----

In the above example, each time the process is executed an alignment file is produced whose name depends
on the actual value of the `x` input.

=== Composite inputs and outputs 

So far we have seen how to declare multiple input and output channels, but each channel was handling 
only one value at time. However Nextflow can handle tuple of values. 

When using channel emitting tuple of values the corresponding input declaration must be declared with a `set` qualifier followed by definition of each single element in the tuple. 

In the same manner output channel emitting tuple of values can be declared using the `set` qualifier 
following by the definition of each tuple element in the tuple. 

[source,nextflow,linenums]
----
reads_ch = Channel.fromFilePairs('data/ggal/*_{1,2}.fq')

process foo {
  input: 
    set val(sample_id), file(sample_files) from reads_ch
  output:
    set val(sample_id), file('sample.bam') into bam_ch
  script:
  """
    your_command_here --reads $sample_id > sample.bam
  """
}

bam_ch.println()
----

==== Exercise 

Modify the script of the previous exercise so that the _bam_ file is named as the given `sample_id`.

== When 

The `when` declaration allows you to define a condition that must be verified in order to execute the process. This can be any expression that evaluates a boolean value.

It is useful to enable/disable the process execution depending the state of various inputs and parameters. For example:

[source,nextflow,linenums]
----
params.dbtype = 'nr'
params.prot = 'data/prots/*.tfa'
proteins = Channel.fromPath(params.prot)

process find {
  input:
  file fasta from proteins
  val type from params.dbtype 

  when:
  fasta.name =~ /^BB11.*/ && type == 'nr'

  script:
  """
  blastp -query $fasta -db nr
  """
}
----

== Directives 

Directive declarations allow the definition of optional settings that affect the execution of the current process without affecting the _semantic_ of the task itself.

They must be entered at the top of the process body, before any other declaration blocks (i.e. `input`, `output`, etc).

Directives are commonly used to define the amount of computing resources to be used or 
other meta directives like that allows the definition of extra information for configuration or 
logging purpose. For example:

[source,nextflow,linenums]
----
process foo {
  cpus 2 
  memory 8.GB 
  container 'image/name'

  script:
  """
  your_command --this --that
  """
}
----


The complete list of directives is available https://www.nextflow.io/docs/latest/process.html#directives[at this link].

==== Exercise 

Modify the script of the previous exercise adding a https://www.nextflow.io/docs/latest/process.html#tag[tag] directive logging the `sample_id` in the execution output. 


